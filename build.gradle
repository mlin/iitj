/*
 * For more details take a look at the Java Libraries chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.4.1/userguide/java_library_plugin.html
 */

plugins {
  id "com.diffplug.spotless" version "6.8.0"
  id "me.qoomon.git-versioning" version "6.2.0"
  id "maven-publish"
}

version = '0.0.0-SNAPSHOT'
gitVersioning.apply {
    refs {
        tag('v(?<version>.*)') {
            version = '${ref.version}${dirty}'
        }
    }

    // optional fallback configuration in case of no matching ref configuration
    rev {
        version = '${commit.timestamp.datetime}-${commit.short}${dirty}'
    }
}

// Apply the java-library plugin to add support for Java Library
apply plugin: 'java-library'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.github.samtools:htsjdk:3.0.0'
    testImplementation 'org.apache.commons:commons-math3:3.6.1'
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

spotless {
  format 'misc', {
    // define the files to apply `misc` to
    target '*.gradle', '*.md', '.gitignore'

    // define the steps to apply to those files
    trimTrailingWhitespace()
    indentWithSpaces()
    endWithNewline()
  }
  java {
    importOrder()
    removeUnusedImports()
    googleJavaFormat('1.8').aosp()
  }
}

task docs(type: Javadoc) {
    source = sourceSets.main.allJava
}

publishing {
    publications {
        mvn(MavenPublication) {
            groupId = 'net.mlin'
            artifactId = 'iitj'

            from components.java
        }
    }

    repositories {
        maven {
            name = 'iitj'
            url = layout.buildDirectory.dir("mvn-repo")
        }
    }
}
